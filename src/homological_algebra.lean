import algebra.category.Module.abelian
import algebra.category.Module.images
import algebra.homology.homology
import algebra.homology.Module
import algebra.homology.homotopy
import algebra.homology.quasi_iso

import for_mathlib.homological_complex_abelian

import .category_theory .linear_algebra

open category_theory category_theory.limits homological_complex

local attribute [instance] category_theory.limits.has_zero_object.has_zero
local attribute [instance]
  category_theory.concrete_category.has_coe_to_sort
  category_theory.concrete_category.has_coe_to_fun

noncomputable
def coker_functor (V : Type*) [category V] [has_zero_morphisms V] [has_cokernels V]
  : arrow V ‚•§ V := {
    obj := Œª f, cokernel f.hom,
    map := Œª f g œÜ, cokernel.map _ _ _ _ œÜ.w.symm,
    map_id' := by { intro, ext, simp, apply category.id_comp },
    map_comp' := by { intros, ext, simp }
  }

lemma coker_map_spec {V : Type*} [category V] [has_zero_morphisms V] [has_cokernels V]
  {A B X Y : V}
  (i : A ‚ü∂ X) (j : B ‚ü∂ Y)
  (f' : A ‚ü∂ B) (f : X ‚ü∂ Y)
  (w1 : i ‚â´ f = f' ‚â´ j)
  : cokernel.œÄ i ‚â´ cokernel.map i j f' f w1 = f ‚â´ cokernel.œÄ j :=
  by { delta cokernel.œÄ coequalizer.œÄ cokernel.map, simp }

section general_abelian_category

universes u u' v v'

parameters {C : Type u} {V : Type v} [category.{u'} C] [category.{v'} V]
parameters {Œπ : Type} {c : complex_shape Œπ}

lemma id_eq_zero_of_iso_zero [has_zero_object V] [has_zero_morphisms V] [has_cokernels V] (X : V)
  : is_isomorphic X 0 ‚Üí ùüô X = 0
| ‚ü®H‚ü© := calc ùüô X = H.hom ‚â´ H.inv : H.hom_inv_id.symm
             ... = H.hom ‚â´ 0     : congr_arg _ (has_zero_object.from_zero_ext H.inv 0)
             ... = 0              : comp_zero

lemma homology_at_ith_index_zero
  [has_zero_object V] [has_zero_morphisms V] [has_equalizers V] [has_cokernels V]
  [has_images V] [has_image_maps V]
  {X Y : homological_complex V c} (f : X ‚ü∂ Y) (i : Œπ) (H : f.f i = 0)
  : (homology_functor V c i).map f = 0 :=
begin
  ext, simp,
  suffices : kernel_subobject_map (homological_complex.hom.sq_from f i) = 0,
  { rw this, simp },
  ext, simp,
  rw H, simp
end

def homological_complex_functor.mk_nat_trans [has_zero_morphisms V]
  {F G : C ‚•§ homological_complex V c}
  (Œ∑ : Œ† (i : Œπ), (F ‚ãô homological_complex.eval V c i) ‚ü∂ (G ‚ãô homological_complex.eval V c i))
  (Œ∑_comm_ds : ‚àÄ (i j : Œπ), c.rel i j ‚Üí
                  ‚àÄ X, (Œ∑ i).app X ‚â´ (G.obj X).d i j = (F.obj X).d i j ‚â´ (Œ∑ j).app X)
  : F ‚ü∂ G := {
    app := Œª X, { f := Œª i, (Œ∑ i).app X,
                  comm' := by exact Œª i j h, Œ∑_comm_ds i j h X },
    naturality' := Œª X Y f, homological_complex.hom.ext _ _ (funext (Œª i, (Œ∑ i).naturality f))
  }

def homological_complex.d_nat_trans [has_zero_morphisms V]
  (F : C ‚•§ homological_complex V c) (i j : Œπ)
  : (F ‚ãô homological_complex.eval V c i) ‚ü∂ (F ‚ãô homological_complex.eval V c j) := {
    app := Œª X, (F.obj X).d i j,
    naturality' := by simp
  }

structure natural_chain_homotopy [preadditive V]
  {F G : C ‚•§ homological_complex V c} (Œ± Œ≤ : nat_trans F G)
  : Type (max u (max v (v' + 1))) :=
(to_chain_htpy : ‚àÄ X, @homotopy Œπ V _ _ c (F.obj X) (G.obj X) (Œ±.app X) (Œ≤.app X))
(naturality : ‚àÄ X Y (f : X ‚ü∂ Y) i j, c.rel i j ‚Üí (F.map f).f j ‚â´ (to_chain_htpy Y).hom j i
                                                = (to_chain_htpy X).hom j i ‚â´ (G.map f).f i)

-- This is why we run into universe issues and need Œπ : Type
-- parallel_pair_comp is an equality between two functors
-- and that constrains them to be in the same universe
protected def parallel_pair_comp_has_colim 
  {V : Type*} [category V] {Œπ : Type} {c : complex_shape Œπ}
  [has_zero_morphisms V] [has_cokernels V]
  {X Y : homological_complex V c} (f : X ‚ü∂ Y) (p : Œπ)
  : has_colimit (parallel_pair f 0 ‚ãô eval V c p) := by {
    rw parallel_pair_comp, dsimp, apply_instance
  }

local attribute [instance] parallel_pair_comp_has_colim

noncomputable
def coker_of_chain_map_at [has_zero_morphisms V] [has_cokernels V]
  {X Y : homological_complex V c} (f : X ‚ü∂ Y) (p : Œπ)
  : cocone (parallel_pair (f.f p) 0) :=
  parallel_pair_comp.cocone_comp_to_cocone_pair (eval V c p : homological_complex V c ‚•§ V) f 0
                                                ((eval V c p).map_cocone
                                                  (colimit.cocone (parallel_pair f 0)))

noncomputable
def coker_of_chain_map_at_is_colimit [has_zero_morphisms V] [has_cokernels V]
  {X Y : homological_complex V c} (f : X ‚ü∂ Y) (p : Œπ)
  : is_colimit (coker_of_chain_map_at f p) :=
    parallel_pair_comp.is_colimit_comp_to_is_colimit_pair _ _ _ _
      (is_colimit_of_preserves (eval V c p) (colimit.is_colimit (parallel_pair f 0)))

noncomputable
def chain_homotopy_on_coker_of_compatible_chain_homotopies
  [preadditive V] [has_cokernels V]
  {A B X Y : homological_complex V c}
  (i : A ‚ü∂ X) (j : B ‚ü∂ Y)
  (f' g' : A ‚ü∂ B) (f g : X ‚ü∂ Y)
  (w1 : f' ‚â´ j = i ‚â´ f) (w2 : g' ‚â´ j = i ‚â´ g)
  (s' : homotopy f' g') (s : homotopy f g)
  (comm : s'.comp_right j = (homotopy.of_eq w1).trans ((s.comp_left i).trans (homotopy.of_eq w2.symm)))
  : homotopy ((coker_functor (homological_complex V c)).map (arrow.hom_mk w1 : arrow.mk i ‚ü∂ arrow.mk j))
             ((coker_functor (homological_complex V c)).map (arrow.hom_mk w2 : arrow.mk i ‚ü∂ arrow.mk j)) := {
  hom := Œª p q, is_colimit.desc
                  (coker_of_chain_map_at_is_colimit i p)
                  (cofork.of_œÄ (s.hom p q ‚â´ cofork.œÄ (coker_of_chain_map_at j q)) 
                  (by { rw [zero_comp, ‚Üê category.assoc],
                        have := congr_arg (Œª h, homotopy.hom h p q) comm, simp at this,
                        rw [‚Üê this, category.assoc, cokernel_cofork.condition, comp_zero] })),
  zero' := by { intros p q h,
                have : i.f p ‚â´ 0 = 0 ‚â´ 0 := eq.trans comp_zero comp_zero.symm,
                transitivity (coker_of_chain_map_at_is_colimit i p).desc (cofork.of_œÄ 0 this),
                { congr, rw s.zero' p q h, exact zero_comp },
                { symmetry,
                  refine (coker_of_chain_map_at_is_colimit i p).uniq' (cofork.of_œÄ 0 this) 0 _,
                  intro u, cases u; simp } },
  comm := by { intro p,
               have H : ‚àÄ (h' : A ‚ü∂ B) (h : X ‚ü∂ Y) (w : h' ‚â´ j = i ‚â´ h),
                        i.f p ‚â´ homological_complex.hom.f h p
                              ‚â´ cofork.œÄ (coker_of_chain_map_at j p)
                      = 0 ‚â´ homological_complex.hom.f h p ‚â´ cofork.œÄ (coker_of_chain_map_at j p),
               { intros h' h w, rw [zero_comp, ‚Üê category.assoc],
                 have := congr_arg (Œª h, homological_complex.hom.f h p) w, dsimp at this,
                 rw [‚Üê this, category.assoc, cokernel_cofork.condition, comp_zero] },
               have : ‚àÄ (h' : A ‚ü∂ B) (h : X ‚ü∂ Y) (w : h' ‚â´ j = i ‚â´ h),
                        ((coker_functor (homological_complex V c)).map
                          (arrow.hom_mk w : arrow.mk i ‚ü∂ arrow.mk j)).f p
                      = is_colimit.desc
                          (coker_of_chain_map_at_is_colimit i p)
                          (cofork.of_œÄ (h.f p ‚â´ cofork.œÄ (coker_of_chain_map_at j p)) (H h' h w)),
               { intros h' h w,
                 apply (coker_of_chain_map_at_is_colimit i p).uniq'
                         (cofork.of_œÄ (h.f p ‚â´ cofork.œÄ (coker_of_chain_map_at j p)) (H h' h w)),
                 intro u, cases u; simp,
                 refine eq.trans (category.assoc _ _ _) _,
                 refine eq.trans _ (category.assoc _ _ _),
                 simp,
                 refine eq.trans (category.id_comp _) _,
                 refine eq.trans _ (congr_arg _ (category.id_comp _).symm),
                 change (cokernel.œÄ i
                         ‚â´ (coker_functor (homological_complex V c)).map
                              (arrow.hom_mk w  : arrow.mk i ‚ü∂ arrow.mk j)).f p
                        = (h ‚â´ cokernel.œÄ j).f p,
                 congr' 1,
                 dsimp [coker_functor],
                 exact coker_map_spec i j h' h w.symm },
               rw [this f' f w1, this g' g w2],
               symmetry, apply is_colimit.uniq'
                                 (coker_of_chain_map_at_is_colimit i p)
                                 (cofork.of_œÄ (f.f p ‚â´ cofork.œÄ (coker_of_chain_map_at j p))
                                              (H f' f w1)),
               intro u, cases u; simp,
               have := congr_arg (Œª t, t ‚â´ cofork.œÄ (coker_of_chain_map_at j p)) (s.comm p),
               refine eq.trans _ this.symm,
               simp,
               delta cofork.œÄ coker_of_chain_map_at parallel_pair_comp.cocone_comp_to_cocone_pair,
               dsimp,
               rw [category.assoc, ‚Üê d_next_comp_left, category.assoc, ‚Üê prev_d_comp_left], 
               apply congr_arg2; simp only [eq_to_hom_app, eq_to_hom_refl],
               { refine eq.trans (category.id_comp _) _,
                 refine eq.trans _ (congr_arg _ (category.id_comp _).symm),
                 rw ‚Üê d_next_comp_right, congr,
                 ext q r,
                 rw (_ : (cokernel.œÄ i).f q
                       = cofork.œÄ (coker_of_chain_map_at i q)),
                 simp,
                 exact congr_arg _ (category.id_comp _),
                 delta cofork.œÄ coker_of_chain_map_at parallel_pair_comp.cocone_comp_to_cocone_pair,
                 simp, },
               { refine eq.trans (category.id_comp _) _,
                 refine eq.trans _ (congr_arg _ (category.id_comp _).symm),
                 rw ‚Üê prev_d_comp_right, congr,
                 ext q r,
                 rw (_ : (cokernel.œÄ i).f q
                       = cofork.œÄ (coker_of_chain_map_at i q)),
                 simp,
                 exact congr_arg _ (category.id_comp _),
                 delta cofork.œÄ coker_of_chain_map_at parallel_pair_comp.cocone_comp_to_cocone_pair,
                 simp, } } }

end general_abelian_category

section chain_complex

universes u u' v v'

parameters {C : Type u} {V : Type v} [category.{u'} C] [category.{v'} V]
parameters [preadditive V] [has_equalizers V] [has_images V] [has_image_maps V] [has_cokernels V]

def chain_complex.mk_natural_chain_homotopy
  {F G : C ‚•§ chain_complex V ‚Ñï}
  {Œ± Œ≤ : F ‚ü∂ G}
  (s : Œ† n, (F ‚ãô homological_complex.eval V _ n) ‚ü∂ (G ‚ãô homological_complex.eval V _ (n + 1)))
  (H0 : ‚àÄ X, (nat_trans.app Œ± X).f 0 = ((s 0).app X ‚â´ (G.obj X).d 1 0) + (nat_trans.app Œ≤ X).f 0)
  (H : ‚àÄ n X, (nat_trans.app Œ± X).f (n + 1)
            = ((F.obj X).d (n + 1) n ‚â´ (s n).app X)
            + ((s (n + 1)).app X ‚â´ (G.obj X).d (n + 2) (n + 1))
            + (nat_trans.app Œ≤ X).f (n + 1))
  : natural_chain_homotopy Œ± Œ≤ := {
    to_chain_htpy := Œª X, { hom := Œª j i, if h : j + 1 = i 
                                          then eq.rec ((s j).app X) h
                                          else 0,
                            zero' := Œª j i h, dite_eq_right_iff.mpr (Œª h', absurd h' h),
                            comm := by { intro n,
                                         cases n,
                                         { simp, exact H0 X },
                                         { simp, exact H n X } } },
    naturality := Œª X Y f i j h, by { dsimp at h, subst h, simp, exact (s j).naturality' f } 
  }

def chain_complex.mk_natural_chain_homotopy_rec 
  {F G : C ‚•§ chain_complex V ‚Ñï}
  {Œ± Œ≤ : F ‚ü∂ G}
  (init : (F ‚ãô homological_complex.eval V _ 0) ‚ü∂ (G ‚ãô homological_complex.eval V _ 1))
  (Hinit : ‚àÄ X, (Œ±.app X).f 0 = (nat_trans.app init X ‚â´ (G.obj X).d 1 0) + (Œ≤.app X).f 0)
  (step : Œ† (n : ‚Ñï) (s : (F ‚ãô homological_complex.eval V _ n)
                       ‚ü∂ (G ‚ãô homological_complex.eval V _ (n + 1))),
            (‚àÄ X, ((Œ±.app X).f (n + 1)) ‚â´ (G.obj X).d (n + 1) n
                = (((F.obj X).d (n + 1) n ‚â´ nat_trans.app s X) + (Œ≤.app X).f (n + 1))
                  ‚â´ (G.obj X).d (n + 1) n)
            ‚Üí ((F ‚ãô homological_complex.eval V _ (n + 1))
              ‚ü∂ (G ‚ãô homological_complex.eval V _ (n + 2))))
  (Hstep : ‚àÄ (n : ‚Ñï) (s : (F ‚ãô homological_complex.eval V _ n)
                       ‚ü∂ (G ‚ãô homological_complex.eval V _ (n + 1)))
             (h : (‚àÄ X, ((Œ±.app X).f (n + 1)) ‚â´ (G.obj X).d (n + 1) n
                = (((F.obj X).d (n + 1) n ‚â´ nat_trans.app s X) + (Œ≤.app X).f (n + 1))
                  ‚â´ (G.obj X).d (n + 1) n)),
             ‚àÄ X, (nat_trans.app Œ± X).f (n + 1)
                = ((F.obj X).d (n + 1) n ‚â´ s.app X)
                + ((step n s h).app X ‚â´ (G.obj X).d (n + 2) (n + 1))
                + (nat_trans.app Œ≤ X).f (n + 1))
  : natural_chain_homotopy Œ± Œ≤ :=
  chain_complex.mk_natural_chain_homotopy
    (Œª k,
      (@nat.rec (Œª n, Œ£' (s : (F ‚ãô homological_complex.eval V _ n)
                            ‚ü∂ (G ‚ãô homological_complex.eval V _ (n + 1))), 
                        ‚àÄ X, ((Œ±.app X).f (n + 1)) ‚â´ (G.obj X).d (n + 1) n
                           = (((F.obj X).d (n + 1) n ‚â´ nat_trans.app s X) + (Œ≤.app X).f (n + 1))
                              ‚â´ (G.obj X).d (n + 1) n)
                ‚ü®init, by { intro X,
                            rw (Œ±.app X).comm',
                            rw Hinit,
                            simp,
                            apply complex_shape.down_mk, refl }‚ü©
                (Œª n p, ‚ü®step n p.1 p.2, 
                        by { intro X,
                             rw (Œ±.app X).comm',
                             rw Hstep n p.1 p.2,
                             simp,
                             apply complex_shape.down_mk, refl }‚ü©)
                k).fst)
    Hinit
    (by { intros n X, simp, apply Hstep })

-- I don't know the rules on when instances should be global
instance [has_zero_object V] (C : chain_complex V ‚Ñï)
  : is_iso (kernel_subobject (C.d_from 0)).arrow :=
  by { rw C.d_from_eq_zero chain_complex.next_nat_zero,
       exact limits.is_iso_kernel_subobject_zero_arrow }

end chain_complex

section Modules

universes u u' v v'

parameters {C : Type u} {R : Type v} [category.{u'} C] [comm_ring R]
parameters {Œπ : Type} {c : complex_shape Œπ}

lemma all_eq_zero_of_iso_zero {M : Module.{v'} R} (H : is_isomorphic M 0) (x : M) : x = 0 :=
  congr_fun (congr_arg coe_fn (id_eq_zero_of_iso_zero M H)) x

lemma iso_zero_of_id_eq_zero (M : Module.{v'} R) (h : ùüô M = 0) : is_isomorphic M 0 :=
    ‚ü®is_zero.iso_zero (@Module.is_zero_of_subsingleton _ _ M
                         ‚ü®Œª x y, calc x = (ùüô M : M ‚ü∂ M) x : rfl
                                    ... = (0 : M ‚ü∂ M) x   : congr_fun (congr_arg _ h) x
                                    ... = (0 : M ‚ü∂ M) y   : rfl
                                    ... = (ùüô M : M ‚ü∂ M) y : (congr_fun (congr_arg _ h) y).symm‚ü©)‚ü©

lemma Module.to_homology.homomorphism (C : homological_complex (Module.{v'} R) c) (i : Œπ)
  : @is_linear_map R (linear_map.ker (C.d_from i)) (C.homology i) _ _ _ _ _
      (@Module.to_homology R _ Œπ c C i) := by {
    delta Module.to_homology, delta Module.to_cycles,
    delta homology.œÄ, delta Module.to_kernel_subobject,
    constructor; intros; simp
  }

lemma Module.to_cycles.homomorphism (C : homological_complex (Module.{v'} R) c) (i : Œπ)
  : is_linear_map R (@Module.to_cycles R _ Œπ c C i) := by {
    delta Module.to_cycles, delta Module.to_kernel_subobject,
    constructor; intros; simp
  }

lemma Module.to_homology_def 
  (C : homological_complex (Module.{v'} R) c) {i : Œπ}
  : is_linear_map.mk' _ (Module.to_homology.homomorphism C i)
  = Module.as_hom_right (is_linear_map.mk' _ (Module.to_cycles.homomorphism C i))
  ‚â´ homology.œÄ (C.d_to i) (C.d_from i) _ := by { ext : 1, refl }

lemma Module.to_cycles_def
  (C : homological_complex (Module.{v'} R) c) {i : Œπ}
  : Module.as_hom_right (is_linear_map.mk' _ (Module.to_cycles.homomorphism C i))
  = (category_theory.limits.kernel_subobject_iso (C.d_from i)
    ‚â™‚â´ Module.kernel_iso_ker (C.d_from i)).inv := by { ext : 1, refl }

lemma Module.to_cycles_is_iso (C : homological_complex (Module.{v'} R) c) (i : Œπ)
  : is_iso (Module.as_hom_right (is_linear_map.mk' _ (Module.to_cycles.homomorphism C i))) :=
  by { rw Module.to_cycles_def, apply is_iso.of_iso_inv }

lemma Module.to_homology_comp_homology_functor_map
  {X Y : homological_complex (Module.{v'} R) c} (f : X ‚ü∂ Y) (i : Œπ)
  : Module.as_hom_right (@is_linear_map.mk' R (linear_map.ker (X.d_from i)) (X.homology i)
                                            _ _ _ _ _ 
                                            Module.to_homology
                                            (Module.to_homology.homomorphism X i))
  ‚â´ (homology_functor (Module R) c i).map f
  = Module.of_hom 
      (linear_map.cod_restrict (linear_map.ker (Y.d_from i)) 
        (linear_map.dom_restrict (f.f i) (linear_map.ker (X.d_from i)))
        (by intros; simp))
  ‚â´ Module.as_hom_right (is_linear_map.mk' Module.to_homology
                                           (Module.to_homology.homomorphism Y i)) :=
begin
  ext, cases x, simp [Module.as_hom_right], delta Module.to_homology, congr,
  transitivity, apply Module.cycles_map_to_cycles, refl
end

-- The version in mathlib fixed v' to be v for some reason
lemma Module.cokernel_œÄ_ext'
  {M N : Module.{v'} R} (f : M ‚ü∂ N) {x y : N} (m : M) (w : x = y + f m)
  : cokernel.œÄ f x = cokernel.œÄ f y := by { subst w, simp }

def Module.range_to_ker {A B C : Module.{v'} R} (f : A ‚ü∂ B) (g : B ‚ü∂ C) (w : f ‚â´ g = 0)
  : Module.of R (linear_map.range f) ‚ü∂ Module.of R (linear_map.ker g) := {
    to_fun := Œª x, ‚ü®x.val, by { obtain ‚ü®x, y, h‚ü© := x, subst h, simp, rw [‚Üê comp_apply, w], refl }‚ü©,
    map_add' := by { rintros ‚ü®x, x', hx‚ü© ‚ü®y, y', hy‚ü©, simp },
    map_smul' := by { rintros r ‚ü®x, x', hx‚ü©, apply subtype.eq, simp },
  }

@[simp]
lemma Module.range_to_ker_subtype_ker
  {A B C : Module.{v'} R} (f : A ‚ü∂ B) (g : B ‚ü∂ C) (w : f ‚â´ g = 0)
  : Module.range_to_ker f g w ‚â´ Module.as_hom_right (linear_map.ker g).subtype
  = Module.as_hom_right (linear_map.range f).subtype := by { ext, refl }

lemma Module.image_to_kernel'_kernel_iso_ker
  {A B C : Module.{v'} R} (f : A ‚ü∂ B) (g : B ‚ü∂ C) (w : f ‚â´ g = 0)
  : (Module.image_iso_range f).inv ‚â´ image_to_kernel' f g w
  =  Module.range_to_ker f g w ‚â´ (Module.kernel_iso_ker g).inv :=
begin
  rw [iso.inv_comp_eq, ‚Üê category.assoc, iso.eq_comp_inv],
  apply (@cancel_mono _ _ _ _ _ _ (Module.mono_as_hom'_subtype (linear_map.ker g)) _ _).mp,
  rw [category.assoc, category.assoc, Module.range_to_ker_subtype_ker],
  simp [Module.as_hom_right, image_to_kernel'], 
  symmetry, apply Module.image_iso_range_hom_subtype
end

-- Possible this proof could be prettified/sped up
lemma Module.to_homology_ext
  {X : homological_complex (Module.{v'} R) c}
  {i j : Œπ}
  {x y : linear_map.ker (X.d_from j)}
  (m : X.X i) (hm : x.val = y.val + X.d i j m) 
  : Module.to_homology x = Module.to_homology y :=
  Module.cokernel_œÄ_ext'
    (image_to_kernel (X.d_to j) (X.d_from j) (X.d_to_comp_d_from j))
    ((image_subobject_iso (X.d_to j)).inv
      ((Module.image_iso_range (X.d_to j)).inv
        ‚ü®X.d i j m, 
          by { by_cases c.rel i j,
               { rw X.d_to_eq h,
                 existsi (X.X_prev_iso h).inv m,
                 simp },
               { rw X.shape' i j h, apply submodule.zero_mem } }‚ü©))
    (by { by_cases c.rel i j,
          { have h1 : X.d i j m ‚àà linear_map.ker (X.d_from j),
            { rw [linear_map.mem_ker, ‚Üê X.X_prev_iso_comp_d_to h,
                  comp_apply, ‚Üê comp_apply, d_to_comp_d_from],
              refl },
            transitivity Module.to_cycles y + Module.to_cycles ‚ü®X.d i j m, h1‚ü©,
            { rw ‚Üê is_linear_map.map_add (Module.to_cycles.homomorphism X j),
              congr, apply subtype.eq, exact hm },
            congr,
            rw [‚Üê comp_apply (iso.inv _), ‚Üê image_to_kernel'_kernel_subobject_iso,
                comp_apply, ‚Üê comp_apply (iso.inv _), Module.image_to_kernel'_kernel_iso_ker],
            apply (kernel_subobject_iso (X.d_from j)).eq_app_inv_of_app_hom_eq,
            apply iso.eq_app_inv_of_app_hom_eq,
            apply subtype.eq, 
            delta Module.to_cycles,
            simp [Module.range_to_ker, Module.to_kernel_subobject, Module.kernel_iso_ker] },
          { rw X.shape' i j h at hm, 
            rw subtype.eq (eq.trans hm (add_zero _)),
            symmetry, refine eq.trans _ (add_zero _),
            have : ‚àÄ h' : X.d i j m ‚àà linear_map.range (X.d_to j), 
                    (‚ü®X.d i j m, h'‚ü© : {t // t ‚àà linear_map.range (X.d_to j)}) = 0,
            { intro, simp, rw X.shape' i j h, refl },
            rw this,
            simp } })

lemma Module.homology_ext''
  (C D : homological_complex (Module.{v'} R) c)
  {h k : C ‚ü∂ D} (i : Œπ)
  (w : ‚àÄ (x : C.X i), C.d_from i x = 0 ‚Üí
       ‚àÉ (j : Œπ) (y : D.X j), h.f i x = k.f i x + D.d j i y)
  : (homology_functor (Module.{v'} R) c i).map h = (homology_functor (Module.{v'} R) c i).map k :=
begin
  ext, cases x with x hx,
  rw [homology_functor_map, homology_functor_map, homology.œÄ_map_apply, homology.œÄ_map_apply],
  delta kernel_subobject_map, dsimp [hom.sq_from],
  rw [Module.cycles_map_to_cycles, Module.cycles_map_to_cycles],
  obtain ‚ü®j, y, hy‚ü© := w x hx,
  exact Module.to_homology_ext y hy
end

lemma homological_complex.range_d_eq
  (C : homological_complex (Module.{v'} R) c) {i j : Œπ} (hij : c.rel i j)
  : linear_map.range (C.d i j)
  = linear_map.range (C.boundaries_to_cycles j ‚â´ (C.cycles j).arrow) :=
begin
  delta boundaries_to_cycles,
  rw image_to_kernel_arrow,
  symmetry, apply ((Module.subobject_Module _).apply_eq_iff_eq_symm_apply _ _).mpr,
  dsimp [Module.subobject_Module, order_iso.symm, rel_iso.symm],
  refine eq.trans _
          (eq.trans (image_subobject_iso_comp (C.X_prev_iso hij).hom (C.d i j))
                    _),
  { congr, apply homological_complex.d_to_eq },
  { refine eq.trans (image_subobject_mono _).symm (eq.trans _ (image_subobject_mono _)),
    refine eq.trans _ (image_subobject_iso_comp (Module.image_iso_range (C.d i j)).hom _),
    congr, symmetry, apply Module.image_iso_range_hom_subtype }
end

def homological_complex.exists_preim_cycle_of_to_homology_zero
  (C : homological_complex (Module.{v'} R) c) {i j : Œπ} (hij : c.rel i j)
  (x : C.X j) (is_cycle : C.d_from j x = 0) (H : Module.to_homology ‚ü®x, is_cycle‚ü© = 0)
  : ‚àÉ t : C.X i, (C.d i j) t = x :=
begin
  apply linear_map.mem_range.mp,
  rw C.range_d_eq hij,
  change (x ‚àà linear_map.range ((C.cycles j).arrow.comp (C.boundaries_to_cycles j))),
  rw linear_map.range_comp (C.boundaries_to_cycles j) (C.cycles j).arrow,
  rw submodule.mem_map,
  refine ‚ü®Module.to_cycles ‚ü®x, is_cycle‚ü©, _, Module.to_kernel_subobject_arrow _‚ü©,
  apply (submodule.quotient.mk_eq_zero _).mp,
  have : function.injective (Module.cokernel_iso_range_quotient (C.boundaries_to_cycles j)).inv,
  { apply function.left_inverse.injective, intro, apply iso.inv_hom_id_apply },
  refine this (eq.trans (eq.trans _ H) (map_zero _).symm),
  delta Module.to_homology, delta homology.œÄ,
  rw ‚Üê Module.range_mkq_cokernel_iso_range_quotient_inv, refl
end

noncomputable
def homological_complex.preim_cycle_of_to_homology_zero
  (C : homological_complex (Module.{v'} R) c) {i j : Œπ} (hij : c.rel i j)
  (x : C.X j) (is_cycle : C.d_from j x = 0) (H : Module.to_homology ‚ü®x, is_cycle‚ü© = 0)
  : C.X i :=
@classical.some (C.X i) (Œª y, C.d i j y = x)
                (homological_complex.exists_preim_cycle_of_to_homology_zero C hij x is_cycle H)

lemma homological_complex.preim_cycle_of_to_homology_zero_spec
  (C : homological_complex (Module.{v'} R) c) {i j : Œπ} (hij : c.rel i j)
  (x : C.X j) (is_cycle : C.d_from j x = 0) (H : Module.to_homology ‚ü®x, is_cycle‚ü© = 0)
  : (C.d i j) (homological_complex.preim_cycle_of_to_homology_zero C hij x is_cycle H) = x :=
@classical.some_spec (C.X i) (Œª y, C.d i j y = x)
                     (homological_complex.exists_preim_cycle_of_to_homology_zero C hij x is_cycle H)

noncomputable
def homological_complex.preim_cycle_of_homology_zero
  (C : homological_complex (Module.{v'} R) c) {i j : Œπ} (hij : c.rel i j)
  (x : C.X j) (is_cycle : C.d_from j x = 0) (H : is_isomorphic (C.homology j) 0) 
  : C.X i :=
homological_complex.preim_cycle_of_to_homology_zero C hij x is_cycle (all_eq_zero_of_iso_zero H _)

lemma homological_complex.preim_cycle_of_homology_zero_spec
  (C : homological_complex (Module.{v'} R) c) {i j : Œπ} (hij : c.rel i j)
  (x : C.X j) (is_cycle : C.d_from j x = 0) (H : is_isomorphic (C.homology j) 0)
  : C.d i j (homological_complex.preim_cycle_of_homology_zero C hij x is_cycle H) = x :=
homological_complex.preim_cycle_of_to_homology_zero_spec C hij x is_cycle
                                                         (all_eq_zero_of_iso_zero H _)

lemma homological_complex.exists_preim_homology_class
  (C : homological_complex (Module.{v'} R) c) {i : Œπ} (y : C.homology i)
  : ‚àÉ (x : C.X i) (h : C.d_from i x = 0), Module.to_homology ‚ü®x, h‚ü© = y :=
begin
  suffices : y ‚àà linear_map.range (is_linear_map.mk' _ (Module.to_homology.homomorphism C i)),
  { obtain ‚ü®‚ü®y, h‚ü©, h'‚ü© := linear_map.mem_range.mp this, existsi y, existsi h, exact h' },
  rw [Module.to_homology_def, Module.comp_def],
  rw linear_map.range_comp,
  rw linear_map.range_eq_top.mpr,
  { rw submodule.map_top, revert y, apply submodule.eq_top_iff'.mp,
    rw linear_map.range_eq_top.mpr,
    delta homology.œÄ,
    rw ‚Üê Module.range_mkq_cokernel_iso_range_quotient_inv,
    rw coe_comp, apply function.surjective.comp,
    refine function.left_inverse.surjective (iso.hom_inv_id_apply _),
    apply submodule.mkq_surjective },
  { exact function.right_inverse.surjective
            (@is_iso.inv_hom_id_apply _ _ _ _ _ (Module.to_cycles_is_iso C i) _) }
end

noncomputable
def homological_complex.preim_of_homology_class
  (C : homological_complex (Module.{v'} R) c) {i : Œπ} (y : C.homology i) 
  : C.X i :=
  classical.some (homological_complex.exists_preim_homology_class C y)

def homological_complex.preim_of_homology_class_spec
  (C : homological_complex (Module.{v'} R) c) {i : Œπ} (y : C.homology i)
  : ‚àÉ (h : C.d_from i (homological_complex.preim_of_homology_class C y) = 0),
      Module.to_homology ‚ü®homological_complex.preim_of_homology_class C y, h‚ü© = y :=
  classical.some_spec (homological_complex.exists_preim_homology_class C y)

lemma Module.to_homology_eq_zero
  {X : homological_complex (Module.{v'} R) c}
  {i j : Œπ} (hij : c.rel i j)
  {x : linear_map.ker (X.d_from j)}
  : x.val ‚àà linear_map.range (X.d i j) ‚Üî Module.to_homology x = 0 :=
begin
  split,
  { rintro ‚ü®w, h‚ü©,
    transitivity Module.to_homology (0 : linear_map.ker (X.d_from j)),
    apply Module.to_homology_ext w, symmetry, simp, exact h,
    apply (Module.to_homology.homomorphism _ _).map_zero },
  { intro h, cases x with x hx,
    obtain ‚ü®y, hy‚ü© := homological_complex.exists_preim_cycle_of_to_homology_zero X hij x hx h,
    exact ‚ü®y, hy‚ü© }
end

lemma Module.to_homology_eq_zero'
  {X : homological_complex (Module.{v'} R) c}
  {i : Œπ} (hi : c.prev i = none)
  {x : linear_map.ker (X.d_from i)}
  : x = 0 ‚Üî Module.to_homology x = 0 :=
begin
  split,
  { intro h, subst h, exact is_linear_map.map_zero (Module.to_homology.homomorphism X i) },
  { intro h, delta Module.to_homology at h,
    suffices : Module.to_cycles x = 0,
    { delta Module.to_cycles Module.to_kernel_subobject at this,
      convert congr_arg (kernel_subobject_iso (X.d_from i) ‚â™‚â´ Module.kernel_iso_ker (X.d_from i)).hom this;
      simp },
    { generalize_hyp : Module.to_cycles x = y at ‚ä¢ h,
      delta homology.œÄ at h,
      suffices : is_iso (cokernel.œÄ (image_to_kernel (X.d_to i) (X.d_from i) (X.d_to_comp_d_from i))),
      { refine eq.trans _ (eq.trans (congr_arg (@inv _ _ _ _ _ this) h) (map_zero _)), simp },
      convert cokernel.œÄ_zero_is_iso,
      all_goals
      { apply zero_of_source_iso_zero,
        refine (image_subobject_iso _).trans _,
        apply category_theory.limits.image_zero',
        delta homological_complex.d_to, rw hi } } }
end

end Modules

section retract

universes v v'
parameters {R : Type v} [comm_ring R]
parameters {Œπ : Type} {c : complex_shape Œπ}


/-
We should be able to prove that iterating barycentric subdivision enough times,
depending on the input simplex, gives a chain map C_*(X) -> C_*(X) which is a retraction onto
the subcomplex of chains bounded by an open cover ùí∞, as in Hatcher. But this subtly requires that
if œÑ is a face of œÉ and B^k(œÉ) ‚àà C_n^ùí∞(X) then B^k(œÑ) ‚àà C_n^ùí∞(X).
This follows from the fact that B is secretly a morphism of simplicial abelian groups
Sing_‚Ä¢(X) ‚Üí Sing_‚Ä¢(X), and so commutes with face maps. But in a general chain complex we don't
have access to the face maps (Dold Kan is *not* about the Moore complex, but the normalized Moore complex)
and so it's awkward to state the lemma. Also proving this would be a lot of work
-/

-- local attribute [instance] classical.prop_decidable 

def homotopy.iterate {V : Type v} [category.{v'} V] [preadditive V] 
  {C : homological_complex V c} {f : C ‚ü∂ C}
  (s : homotopy (ùüô C) f)
  : Œ† (k : ‚Ñï), homotopy (ùüô C) (f ^ k : End C)
| 0       := homotopy.refl (ùüô C)
| (k + 1) := (homotopy.iterate k).trans (s.symm.comp_left_id (f ^ k : End C)).symm

lemma chain_map_iterate {V : Type v} [category.{v'} V] [has_zero_morphisms V]
  {C : homological_complex V c} (f : C ‚ü∂ C)
  (k : ‚Ñï) (i : Œπ) : (f ^ k : End C).f i = (f.f i ^ k : End (C.X i)) :=
begin
  induction k with k ih,
  { refl },
  { rw [‚Üê npow_eq_pow, ‚Üê npow_eq_pow, monoid.npow_succ', monoid.npow_succ'],
    exact congr_arg2 category_struct.comp ih rfl }
end

-- lemma homotopy.iterate_as_sum {V : Type v} [category.{v'} V] [preadditive V]
--   {C : homological_complex V c} {f : C ‚ü∂ C}
--   (s : homotopy (ùüô C) f) (k : ‚Ñï) (i j : Œπ)
--   : (s.iterate k).hom i j = finset.univ.sum (Œª p : fin k, (f^(p : ‚Ñï) : End C).f i ‚â´ s.hom i j) :=
-- begin
--   by_cases c.rel j i,
--   { induction k with k ih,
--     { refl },
--     { simp [homotopy.iterate],
--       rw fin.sum_univ_cast_succ,
--       simp,
--       rw ih,
--       congr,
--       simp [homotopy.symm, homotopy.comp_left_id] } },
--   { rw (s.iterate k).zero i j h, simp_rw s.zero i j h, simp }
-- end

-- noncomputable
-- def homotopy.iter_until_in_subcomplex
--   {Œπ' : Œπ ‚Üí Type}
--   {C : homological_complex (Module.{v'} R) c}
--   (b : Œ† (i : Œπ), basis (Œπ' i) R (C.X i))
--   (M : Œ† (i : Œπ), submodule R (C.X i))
--   (f : C ‚ü∂ C)
--   (H : ‚àÄ i x, ‚àÉ k, (f.f i)^[k] x ‚àà M i)
--   (s : homotopy (ùüô C) f) (i j : Œπ) : C.X i ‚ü∂ C.X j :=
--   (b i).constr R (Œª ‚Ñì, (s.iterate (nat.find (H i (b i ‚Ñì)))).hom i j (b i ‚Ñì))

-- lemma d_next_of_iter_until_in_subcomplex_on_basis 
--   {Œπ' : Œπ ‚Üí Type}
--   {C : homological_complex (Module.{v'} R) c}
--   (b : Œ† (i : Œπ), basis (Œπ' i) R (C.X i))
--   (M : Œ† (i : Œπ), submodule R (C.X i))
--   (f : C ‚ü∂ C)
--   (H : ‚àÄ i x, ‚àÉ k, (f.f i)^[k] x ‚àà M i)
--   (s : homotopy (ùüô C) f) (i : Œπ) (‚Ñì : Œπ' i)
--   : prev_d i (homotopy.iter_until_in_subcomplex b M f H s) (b i ‚Ñì)
--   = prev_d i (s.iterate (nat.find (H i (b i ‚Ñì)))).hom (b i ‚Ñì) :=
-- begin
-- destruct c.prev i,
-- { intro h, delta prev_d, simp, rw h },
-- { rintros ‚ü®j, hij‚ü© h, delta prev_d, simp, rw h, simp,
--   refine congr_arg _ _,
--   exact basis.constr_basis _ _ _ _ }
-- end

-- noncomputable
-- def retract_free_of_eventually_in_submodule
--   {Œπ' : Œπ ‚Üí Type}
--   (C : homological_complex (Module.{v'} R) c)
--   (b : Œ† (i : Œπ), basis (Œπ' i) R (C.X i))
--   (M : Œ† (i : Œπ), submodule R (C.X i))
--   (f : C ‚ü∂ C)
--   (H : ‚àÄ i x, ‚àÉ k, (f.f i)^[k] x ‚àà (M i))
--   (s : homotopy (ùüô C) f)
--   : C ‚ü∂ C := {
--     f := Œª i, (ùüô (C.X i)) - (d_next i) (homotopy.iter_until_in_subcomplex b M f H s)
--                           - (prev_d i) (homotopy.iter_until_in_subcomplex b M f H s),
--     comm' := by {
--       intros i j hij,
--       apply (b i).ext, intro ‚Ñì,
--       simp, rw sub_right_comm,
--       congr, 
--       { repeat { rw ‚Üê comp_apply },
--         refine congr_fun (congr_arg coe_fn _) _,
--         transitivity (0 : C.X i ‚ü∂ C.X j),
--         { rw ‚Üê d_from_comp_X_next_iso _ hij,
--           swap, apply_instance, 
--           rw ‚Üê category.assoc (C.d_to i), simp },
--         { rw ‚Üê X_prev_iso_comp_d_to _ hij,
--           rw [category.assoc, ‚Üê category.assoc (C.d_to j)], simp } },
--       { repeat { rw ‚Üê comp_apply },
--         refine congr_fun (congr_arg coe_fn _) _,
--         rw [‚Üê category.assoc, ‚Üê d_next_eq_d_from_from_next, d_next_eq _ hij],
--         rw category.assoc, refine congr_arg _ _,
--         rw ‚Üê prev_d_eq _ hij, simp } } }

-- def retract_free_of_eventually_in_submodule_eventually_in
--   {Œπ' : Œπ ‚Üí Type}
--   (C : homological_complex (Module.{v'} R) c)
--   (b : Œ† (i : Œπ), basis (Œπ' i) R (C.X i))
--   (M : Œ† (i : Œπ), submodule R (C.X i))
--   (f : C ‚ü∂ C)
--   (H : ‚àÄ i x, ‚àÉ k, (f.f i)^[k] x ‚àà M i)
--   (s : homotopy (ùüô C) f)
--   (hb : ‚àÄ i j, submodule.map (C.d i j) (M i) ‚â§ M j)
--   (hs : ‚àÄ i j, submodule.map (s.hom i j) (M i) ‚â§ M j)
--   (hf : ‚àÄ i, submodule.map (f.f i) (M i) ‚â§ M i)
--   (hboundary_in_M_only_if : ‚àÄ i ‚Ñì, b i ‚Ñì ‚àà M i
--                           ‚Üí ‚àÄ j m, (b j).repr (C.d i j (b i ‚Ñì)) m ‚â† 0 ‚Üí b j m ‚àà M j)
--   : ‚àÄ i, linear_map.range ((retract_free_of_eventually_in_submodule C b M f H s).f i)
--        ‚â§ M i :=
-- begin
--   have : ‚àÄ i ‚Ñì j m, (b j).repr (C.d i j (b i ‚Ñì)) m ‚â† 0
--                   ‚Üí nat.find (H j (b j m)) ‚â§ nat.find (H i (b i ‚Ñì)),
--   { intros i ‚Ñì j m h,
--     apply nat.find_le,
--     have := nat.find_spec (H i (b i ‚Ñì)), revert this,
--     generalize : nat.find (H i (b i ‚Ñì)) = k, intro h,
--      },
--   intro i,
--   rw linear_map.range_eq_map,
--   rw ‚Üê (b i).span_eq,
--   rw submodule.map_span_le,
--   rintros x ‚ü®‚Ñì, h‚ü©, subst h,
--   dsimp [retract_free_of_eventually_in_submodule],
--   rw d_next_of_iter_until_in_subcomplex_on_basis,
--   have := (s.iterate (nat.find (H i (b i ‚Ñì)))).comm i,
--   rw ‚Üê sub_eq_iff_eq_add at this,
--   symmetry' at this, rw add_comm at this, symmetry' at this, rw ‚Üê sub_eq_iff_eq_add at this,
--   rw ‚Üê this,
--   simp,
--   rw [sub_right_comm, sub_sub _ _ ((from_next i (s.iterate _).hom _)), ‚Üê sub_add],
--   simp, rw add_sub_assoc,
--   apply submodule.add_mem,
--   { convert nat.find_spec (H i (b i ‚Ñì)),
--     generalize : nat.find (H i (b i ‚Ñì)) = k,
--     induction k with k ih,
--     { refl },
--     { rw nat.iterate_succ,
--       rw ‚Üê npow_eq_pow,
--       rw monoid.npow_succ',
--       rw ‚Üê ih,
--       refl } },
--   {  }
--   -- refine eq.trans (congr_arg2 _ _ _) (add_zero _),
--   -- { transitivity (f ^ 0 : End C).f i (b i ‚Ñì),
--   --   { congr, simp,
--   --     exact submodule.subset_span ‚ü®‚Ñì, h‚Ñì, rfl‚ü© },
--   --   { refl } },
-- end

-- def retract_free_of_eventually_in_submodule_homotopic
--   {Œπ' : Œπ ‚Üí Type}
--   (C : homological_complex (Module.{v'} R) c)
--   (b : Œ† (i : Œπ), basis (Œπ' i) R (C.X i))
--   (b' : Œ† (i : Œπ), Œπ' i ‚Üí Prop)
--   (f : C ‚ü∂ C)
--   (H : ‚àÄ i x, ‚àÉ k, (f.f i)^[k] x ‚àà submodule.span R (b i '' { ‚Ñì | b' i ‚Ñì }))
--   (s : homotopy (ùüô C) f)
--   (hf : ‚àÄ (i : Œπ) (‚Ñì : Œπ' i), f.f i (b i ‚Ñì) ‚àà submodule.span R (b i '' { m | b' i m }))
--   (hb' : ‚àÄ (i j : Œπ) (‚Ñì : Œπ' i), C.d i j (b i ‚Ñì) ‚àà submodule.span R (b j '' { m | b' j m }))
--   : homotopy (ùüô C) (retract_free_of_eventual_retract_on_basis C b (Œª j, submodule.span R (b j '' { ‚Ñì | b' j ‚Ñì })) f H s) :=
-- begin
--   admit
-- end

end retract

section Modules

universes u u' v v'

parameters {C : Type u} {R : Type v} [category.{u'} C] [comm_ring R]
parameters {Œπ : Type} {c : complex_shape Œπ}

def Module.subcomplex_of_compatible_submodules
  (C : homological_complex (Module.{v'} R) c)
  (M : Œ† (i : Œπ), submodule R (C.X i))
  (hcompat : ‚àÄ i j, submodule.map (C.d i j) (M i) ‚â§ M j)
  : homological_complex (Module.{v'} R) c := {
    X := Œª i, Module.of R (M i),
    d := Œª i j, linear_map.cod_restrict (M j) 
                                        (linear_map.dom_restrict (C.d i j) (M i))
                                        (Œª x, hcompat i j (submodule.mem_map_of_mem x.property)),
    d_comp_d' := by { intros i j k hij hjk, ext, cases x with x hx, 
                      simp, rw ‚Üê comp_apply, rw C.d_comp_d, refl },
    shape' := by { intros i j hij, ext, cases x with x hx, simp, rw C.shape' i j hij, refl } }

def Module.subcomplex_of_compatible_submodules_inclusion
  (C : homological_complex (Module.{v'} R) c)
  (M : Œ† (i : Œπ), submodule R (C.X i))
  (hcompat : ‚àÄ i j, submodule.map (C.d i j) (M i) ‚â§ M j)
  : Module.subcomplex_of_compatible_submodules C M hcompat ‚ü∂ C := {
    f := Œª i, (M i).subtype
  }

lemma quasi_iso_of_lift_boundaries_and_cycles
  {C D : homological_complex (Module.{v'} R) c} (f : C ‚ü∂ D)
  (h1 : ‚àÄ i j x y, C.d_from j y = 0 ‚Üí D.d i j x = f.f j y ‚Üí ‚àÉ z, C.d i j z = y)
  (h2 : ‚àÄ j x, D.d_from j x = 0 ‚Üí ‚àÉ i y z, C.d_from j y = 0 ‚àß f.f j y = x + D.d i j z)
  : quasi_iso f :=
begin
  constructor,
  intro i,
  suffices : function.bijective ((homology_functor (Module R) c i).map f),
  { let f' := linear_equiv.of_bijective _ this.left this.right,
    constructor,
    refine exists.intro f'.symm _,
    split; ext, { apply f'.left_inv }, { apply f'.right_inv } },
  split,
  { rw [‚Üê linear_map.ker_eq_bot, linear_map.ker_eq_bot'],
    intros x hx,
    obtain ‚ü®h, h'‚ü© := C.preim_of_homology_class_spec x,
    generalize_hyp : C.preim_of_homology_class x = x' at h h', subst h',
    have := congr_arg linear_map.to_fun (@Module.to_homology_comp_homology_functor_map R _ Œπ c C D f i),
    replace this := congr_fun this (‚ü®x', h‚ü© : linear_map.ker (C.d_from i)),
    replace this := eq.trans this.symm hx,
    simp [Module.as_hom_right] at this,
    destruct (c.prev i),
    { intro h', rw ‚Üê Module.to_homology_eq_zero' h' at this ‚ä¢,
      obtain ‚ü®z, hz‚ü© := h1 i i 0 x' h (eq.trans (map_zero _) (subtype.ext_iff_val.mp this.symm)),
      have : ¬¨ c.rel i i, { intro hi', rw c.prev_eq_some hi' at h', injection h' },
      rw C.shape i i this at hz, symmetry, exact subtype.eq hz },
    { rintro ‚ü®j, hij‚ü© _,
      rw ‚Üê Module.to_homology_eq_zero hij,
      rw ‚Üê Module.to_homology_eq_zero hij at this,
      simp at this, obtain ‚ü®y, hy‚ü© := this,
      exact h1 j i y x' h hy } },
  { intro x,
    obtain ‚ü®h, h'‚ü© := D.preim_of_homology_class_spec x,
    generalize_hyp : D.preim_of_homology_class x = x' at h h', subst h',
    obtain ‚ü®j, y, z, hy, hz‚ü© := h2 i x' h,
    existsi Module.to_homology ‚ü®y, hy‚ü©,
    have := congr_arg linear_map.to_fun (@Module.to_homology_comp_homology_functor_map R _ Œπ c C D f i),
    replace this := congr_fun this (‚ü®y, hy‚ü© : linear_map.ker (C.d_from i)),
    refine eq.trans this _,
    exact Module.to_homology_ext z hz }
end

lemma subcomplex_inclusion_quasi_iso_of_pseudo_projection
  {C : homological_complex (Module.{v'} R) c}
  (M : Œ† (i : Œπ), submodule R (C.X i))
  (hcompat : ‚àÄ i j, submodule.map (C.d i j) (M i) ‚â§ M j)
  (p : C ‚ü∂ C) (s : homotopy (ùüô C) p)
  (hp_eventual : ‚àÄ i x, ‚àÉ k, (p.f i)^[k] x ‚àà M i)
  (hp : ‚àÄ i, submodule.map (p.f i) (M i) ‚â§ M i)
  (hs : ‚àÄ i j, submodule.map (s.hom i j) (M i) ‚â§ M j)
  : quasi_iso (Module.subcomplex_of_compatible_submodules_inclusion C M hcompat) :=
begin
  have hp_iter : ‚àÄ i k {x}, x ‚àà M i ‚Üí ((p.f i)^[k] x) ‚àà M i, 
  { intros i k x hx, induction k with k ih,
    { exact hx },
    { rw nat.iterate_succ,
      refine hp i _,
      existsi ((p.f i)^[k] x), exact ‚ü®ih, rfl‚ü© } },
  have hs_iter : ‚àÄ i j k, submodule.map ((s.iterate k).hom i j) (M i) ‚â§ M j,
  { intros i j k, specialize hs i j,
    rw submodule.map_le_iff_le_comap at ‚ä¢ hs,
    intros x hx, simp,
    induction k with k ih,
    { exact zero_mem _, },
    { simp [homotopy.iterate],
      apply submodule.add_mem,
      { exact ih },
      { refine hs _, rw chain_map_iterate p k,
        rw concrete_category.pow_eq_iter, 
        exact hp_iter i k hx } } },
  apply quasi_iso_of_lift_boundaries_and_cycles, 
  { intros i j x y hy hx,
    obtain ‚ü®k, hk‚ü© := hp_eventual i x,
    refine exists.intro (‚ü®(s.iterate k).hom j i y.val, _‚ü© + ‚ü®(p.f i)^[k] x, hk‚ü©) _,
    { refine hs_iter j i k _, exact submodule.mem_map_of_mem y.property },
    { ext,
      delta Module.subcomplex_of_compatible_submodules,
      delta Module.subcomplex_of_compatible_submodules_inclusion at hx,
      simp at ‚ä¢ hx,
      refine eq.trans _ hx,
      by_cases (c.rel i j),
      { rw ‚Üê hx,
        rw ‚Üê comp_apply _ ((s.iterate k).hom j i), 
        rw ‚Üê d_next_eq _ h,
        dsimp,
        have := (s.iterate k).comm i,
        rw ‚Üê sub_eq_iff_eq_add at this,
        rw ‚Üê sub_eq_iff_eq_add at this,
        rw ‚Üê this,
        simp,
        rw (_ : (C.d i j) (C.d_to i (to_prev i (s.iterate k).hom x)) = 0),
        rw sub_zero,
        rw sub_add,
        convert sub_zero _,
        { rw sub_eq_zero,
          refine congr_arg _ _,
          rw chain_map_iterate,
          rw concrete_category.pow_eq_iter },
        { rw ‚Üê homological_complex.d_from_comp_X_next_iso _ h,
          rw ‚Üê comp_apply,
          rw ‚Üê category.assoc,
          simp, apply_instance } },
      { rw C.shape' i j h, simp } } },
  { intros j x hx,
    obtain ‚ü®k, hk‚ü© := hp_eventual j x,
    have : x - ((p.f j)^[k] x) = prev_d j (s.iterate k).hom x,
    { have := (s.iterate k).comm j,
      rw ‚Üê sub_eq_iff_eq_add at this,
      transitivity d_next j (s.iterate k).hom x + prev_d j (s.iterate k).hom x,
      { convert congr_fun (congr_arg coe_fn this) x,
        rw [chain_map_iterate, concrete_category.pow_eq_iter] },
      { symmetry,
        convert add_zero _,
        destruct (c.next j),
        { intro h, delta d_next, rw h, simp },
        { rintros ‚ü®i, h‚ü© _,
          rw d_next_eq _ h,
          dsimp,
          rw ‚Üê d_from_comp_X_next_iso _ h,
          dsimp, rw hx, simp } } },
    rw exists_comm, refine ‚ü®‚ü®_, hk‚ü©, _‚ü©,
    simp_rw [exists_and_distrib_left],
    split, 
    { destruct (c.next j),
      { intro h', delta homological_complex.d_from, rw h', simp },
      { rintros ‚ü®‚Ñì, h'‚ü© _, rw d_from_eq _ h', simp,
        convert map_zero _,
        dsimp [Module.subcomplex_of_compatible_submodules],
        apply subtype.eq, simp,
        rw [‚Üê concrete_category.pow_eq_iter, ‚Üê chain_map_iterate],
        rw [‚Üê comp_apply, homological_complex.hom.comm, comp_apply],
        rw [‚Üê d_from_comp_X_next_iso _ h', comp_apply, hx], simp } },
    destruct (c.prev j),
    { intro h,
      existsi j, refine ‚ü®0, _‚ü©,
      simp,
      delta prev_d at this, rw h at this, simp at this,
      rw sub_eq_zero at this, symmetry' at this, exact this },
    { rintros ‚ü®i, hi‚ü© _, existsi i,
      rw prev_d_eq _ hi at this, dsimp at this, rw sub_eq_iff_eq_add at this,
      existsi - (s.iterate k).hom j i x,
      rw [map_neg, ‚Üê sub_eq_add_neg, eq_sub_iff_add_eq],
      symmetry, rw add_comm, exact this } }
end

end Modules
